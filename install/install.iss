; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Global7500 OMS-IMS"
#define MyAppShortName "G7000OMS"
#define MyAppLCShortName "g7000oms"
#define MyAppVersion "2.2.8beta"
#define MyAppPublisher "CAE"
#define MyAppURL "https://www.cae.com/"
#define MyAppExeName "global7500-oms-ims-trainer-client"

#define MyAppIcon "icon.ico"

#define NSSM "nssm-x64.exe"
#define NSSM32 "nssm-x86.exe"
#define NSSM64 "nssm-x64.exe"
#define NODE32 "node-v12.14.1-x86.msi"
#define NODE64 "node-v12.14.1-x64.msi"
#define NODE "node-v12.14.1-x64.msi"
;to be updated once the branch has been pulled
#define DATAFOLDER "C:\dev\desktop-build\Global7000-OMS-IMS\"


[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{97DA62F5-2E8C-4B96-BB42-61ED2BBF34DF}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=C:\{#MyAppShortName}
DisableDirPage=yes
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
;LicenseFile={#DATAFOLDER}\Downloads\LICENSE.txt
;InfoBeforeFile={#DATAFOLDER}\Downloads\before.txt
;InfoAfterFile={#DATAFOLDER}\Downloads\after.txt
OutputDir={#DATAFOLDER}\bin
OutputBaseFilename={#MyAppShortName}Installer
SetupIconFile={#DATAFOLDER}\install\{#MyAppIcon}
Compression=lzma
SolidCompression=yes


[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"


[Files]
Source: "{#DATAFOLDER}\install\{#NODE64}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#DATAFOLDER}\install\{#NSSM64}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#DATAFOLDER}\install\{#NSSM32}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#DATAFOLDER}\install\{#MyAppIcon}"; DestDir: "{app}"; Flags: ignoreversion
Source: "{#DATAFOLDER}\trainer-dataservices\*"; DestDir: "{app}\server"; Excludes: ".git\*" ; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "{#DATAFOLDER}\install\client-bin\*"; DestDir: "{app}\client"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files


[Icons]
Name: "{commonprograms}\{#MyAppName}"; Filename: "{app}\client\{#MyAppExeName}.exe"; IconFilename: "{app}\{#MyAppIcon}"
Name: "{userdesktop}\{#MyAppExeName}"; Filename: "{app}\client\{#MyAppExeName}.exe"; IconFilename: "{app}\{#MyAppIcon}"


[Run]
; These all run with 'runascurrentuser' (i.e. admin) whereas 'runasoriginaluser' would refer to the logged in user
; Install Node
Filename: "{sys}\msiexec.exe"; Parameters: "/passive /i ""{app}\{#NODE}""";

; Add Firewall Rules
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall add rule name=""Node In"" program=""{pf64}\nodejs\node.exe"" dir=in action=allow enable=yes"; Flags: runhidden;
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall add rule name=""Node Out"" program=""{pf64}\nodejs\node.exe"" dir=out action=allow enable=yes"; Flags: runhidden;

; Add System Service
Filename: "{app}\{#NSSM}"; Parameters: "install {#MyAppShortName} ""{pf64}\nodejs\node.exe"" ""{app}\server\server.js"" "; Flags: runhidden;
Filename: "{sys}\net.exe"; Parameters: "start {#MyAppShortName}"; Flags: runhidden;


[UninstallRun]
; Removes System Service
Filename: "{sys}\net.exe"; Parameters: "stop {#MyAppShortName}"; Flags: runhidden;
Filename: "{app}\{#NSSM}"; Parameters: "remove {#MyAppShortName} confirm"; Flags: runhidden;
Filename: "{sys}\net.exe"; Parameters: "stop {#MyAppLCShortName}"; Flags: runhidden;
;Filename: "{pf64}\nodejs\node.exe"; Parameters: "{app}\bin\windows-service-installer.js remove"; Flags: runhidden;

; Remove Firewall Rules
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall delete rule name=""Node In"" program=""{pf64}\nodejs\node.exe"""; Flags: runhidden;
Filename: "{sys}\netsh.exe"; Parameters: "advfirewall firewall delete rule name=""Node Out"" program=""{pf64}\nodejs\node.exe"""; Flags: runhidden;

; Uninstall Node
Filename: "{sys}\msiexec.exe"; Parameters: "/passive /x ""{app}\{#NODE}""";

; Remove all leftovers
Filename: "{sys}\rmdir"; Parameters: "-r ""{app}""";